plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '5.0.14'
    id 'jacoco'
}

group = 'com.ll'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // S3 의존성
    implementation platform("software.amazon.awssdk:bom:2.20.162")
    implementation "software.amazon.awssdk:s3"
    // Jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // Spring dev-tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // mysql
    runtimeOnly 'com.mysql:mysql-connector-j'
    testRuntimeOnly 'com.h2database:h2'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Oauth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.json:json:20231013'

    // CoolSMS
    implementation 'net.nurigo:sdk:4.3.0'

    // jwt
    implementation "io.jsonwebtoken:jjwt-api:0.12.6"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.12.6"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.12.6"

    //websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'org.hibernate.validator:hibernate-validator:8.0.0.Final'

    // 엘라스틱 서치
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'


    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = true // XML 보고서 생성
        html.required = true // HTML 보고서 생성
    }
}

checkstyle {
    ignoreFailures = true
    configFile = file("config/checkstyle/naver-checkstyle-rules.xml") // 규칙 파일 위치
    configProperties = ["suppressionFile": "checkstyle/naver-checkstyle-suppressions.xml"] // 제외 규칙 파일
}


tasks.withType(Pmd).configureEach {
    reports {
        xml.required = true
        html.required = true
    }
}

pmd {
    ignoreFailures = true
    toolVersion = "7.0.0"
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    reports {
        xml.required = true
        html.required = true
    }
}

spotbugs {
    ignoreFailures = true
}

jacocoTestReport {
    dependsOn test // 테스트 실행 후 리포트 생성
    reports {
        xml.required = true
        html.required = true
    }

    // JaCoCo가 생성하는 report에서 entity와 dto 클래스를 제외
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/entity/**',
                    '**/dto/**',
                    '**/*Application*',
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    // 커버리지의 범위와 퍼센테이지를 설정
    violationRules {
        rule {
            enabled = true // 규칙 활성화 여부
            element = 'CLASS' // 커버리지를 체크할 단위 설정

            // 코드 커버리지를 측정할 때 사용되는 지표
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }

            // 커버리지 검증 과정에서 entity와 dto 클래스를 제외
            excludes = [
                    '**.entity.**',
                    '**.dto.**',
                    '**.*Application*',  // 메인 애플리케이션 클래스
            ]
        }
    }
}
